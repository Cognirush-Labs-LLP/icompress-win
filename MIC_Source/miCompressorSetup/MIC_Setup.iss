; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MICAppVersion "4.0.2"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{E589C232-B690-4416-B731-42E179FEFB60}}
AppName=Mass Image Compressor
AppVersion={#MICAppVersion}
;AppVerName=Mass Image Compressor 4.x
AppPublisher=Cognirush Labs LLP
AppPublisherURL=https://mic.cognirush.com/windows/
AppSupportURL=https://mic.cognirush.com/windows/
AppUpdatesURL=https://mic.cognirush.com/windows/
DefaultDirName={localappdata}\Mass Image Compressor V4
UninstallDisplayIcon={app}\Mass Image Compressor.exe
; "ArchitecturesAllowed=x64compatible" specifies that Setup cannot run
; on anything but x64 and Windows 11 on Arm.
ArchitecturesAllowed=x64compatible
; "ArchitecturesInstallIn64BitMode=x64compatible" requests that the
; install be done in "64-bit mode" on x64 or Windows 11 on Arm,
; meaning it should use the native 64-bit Program Files directory and
; the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64compatible
DisableProgramGroupPage=yes
LicenseFile=.\dist\LICENSE.txt
PrivilegesRequired=lowest
OutputBaseFilename=MassImageCompressorInstallerV4_0_2
SetupIconFile=.\assets\mic_win_4_setup.ico
SolidCompression=yes
WizardStyle=modern
CreateUninstallRegKey=yes

VersionInfoVersion={#MICAppVersion}
VersionInfoProductVersion={#MICAppVersion}
VersionInfoDescription=Mass Image Compressor Installer
VersionInfoCompany=Cognirush Labs LLP
VersionInfoProductName=Mass Image Compressor
VersionInfoTextVersion=Mass Image Compressor {#MICAppVersion}

[UninstallDelete]
Type: filesandordirs; Name: "{localappdata}\Mass Image Compressor V4"
Type: filesandordirs; Name: "{localappdata}\miCompressor\user_config"

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: ".\dist\Mass Image Compressor.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\dist\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{userprograms}\Mass Image Compressor V4"; Filename: "{app}\Mass Image Compressor.exe"
Name: "{userdesktop}\Mass Image Compressor V4"; Filename: "{app}\Mass Image Compressor.exe"; Tasks: desktopicon

[Code]
function InitializeSetup(): Boolean;
var
  InstalledVersion: string;
begin
  if RegQueryStringValue(HKCU, 'Software\Microsoft\Windows\CurrentVersion\Uninstall\{E589C232-B690-4416-B731-42E179FEFB60}_is1', 'DisplayVersion', InstalledVersion) then
  begin
    if InstalledVersion = '{#MICAppVersion}' then
    begin
      if WizardSilent then
      begin
        Log('Mass Image Compressor version ' + '{#MICAppVersion}' + ' is already installed. Installation aborted silently.');
        Result := False;
        Exit;
      end
      else
      begin
        MsgBox('Mass Image Compressor version ' + '{#MICAppVersion}' + ' is already installed.', mbInformation, MB_OK);
        Result := False;
        Exit;
      end;
    end;
  end;

  Result := True;
end;


procedure DeinitializeUninstall();
begin
  DeleteFile(ExpandConstant('{userappdata}\Microsoft\Windows\SendTo\Mass Image Compressor.lnk'));
end;
